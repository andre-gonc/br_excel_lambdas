=LAMBDA(pais_procurados; lista_pais; lista_filhos;
    LET(
        _tabela_lookup; AGRUPARPOR(
            PARACOL(lista_pais; 3);
            lista_filhos;
            LAMBDA(array_interno; UNIRTEXTO("|"; 1; array_interno));
            0;0
        );
        _explodir; LAMBDA(self;itens_atuais;
            LET(
                string_antes; UNIRTEXTO("|"; 1; itens_atuais);
                string_depois; UNIRTEXTO("|"; 1; MAP(itens_atuais; LAMBDA(item; SEERRO(PROCV(item; _tabela_lookup; 2; 0); item))));
                SE(
                    string_antes = string_depois;
                    DIVIDIRTEXTO(string_antes; ; "|");
                    self(self; DIVIDIRTEXTO(string_depois; ; "|"))
                )
            )
        );
        _resultado_bruto; _explodir(_explodir; PARACOL(pais_procurados; 3));
        CLASSIFICAR(ÃšNICO(_resultado_bruto))
    )
)
